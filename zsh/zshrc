# ヒストリの設定
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000

autoload -Uz colors; colors
case ${OSTYPE} in
	darwin*)
		export CLICOLOR=1
		alias ls='ls -G -F'
		;;
	linux*)
		alias ls='ls -F --color=auto'
		;;
esac

########################################
# vcs_info
autoload -Uz vcs_info
autoload -Uz add-zsh-hook

setopt prompt_subst
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr "%F{lime}"
zstyle ':vcs_info:git:*' unstagedstr "%F{yellow}"
zstyle ':vcs_info:*' formats '%F{green}%c%u[%s]-<%b>%f'
zstyle ':vcs_info:*' actionformats '%F{red}[%s]-<%b|%a>%f'

precmd() {
  LANG=en_US.UTF-8 vcs_info
  local left="%F{green}* %D %*>%{${reset_color}%} %~"
  local right="${vcs_info_msg_0_}"

  local invisible='%([BSUbfksu]|([FK]|){*})'
  local leftwidth=${#${(S%%)left//$~invisible/}}
  local rightwidth=${#${(S%%)right//$~invisible/}}
  local padwidth=$(($COLUMNS - ($leftwidth + $rightwidth) % $COLUMNS))

  print -P $left${(r:$padwidth:: :)}$right
}

PROMPT="%# "

########################################

for f in $CONFIG_DIR/snippets/*; do source $f; done


autoload -Uz compinit && compinit
autoload -U +X bashcompinit && bashcompinit

